/* Copyright (c) Advanced Micro Devices, Inc. All rights reserved.
 *
 * SPDX-License-Identifier: MIT
 */

#ifndef NVME_EP_H
#define NVME_EP_H

#include <cstdint>
#include <cstring>
#include <string>
#include <vector>

#include <hip/hip_runtime.h>

#include "xio.h"
/*
 * NVMe Definitions for rocm-xio nvme-ep Endpoint
 */
#include "nvme-ep-generated.h"

namespace xio::nvme_ep {

/**
 * Polling limits for completion queue operations
 */
#define NVME_EP_MAX_POLLS 10000000U            // Maximum polls before timeout
#define NVME_EP_MAX_POLLS_BEFORE_BACKOFF 1000U // Polls before backoff reset

/**
 * Doorbell register constants
 */
constexpr uint32_t doorbellBase = 0x1000U; // Base offset in BAR0
constexpr uint32_t doorBellStride = 4U;    // Bytes per doorbell register

/**
 * NVMe Admin command response size
 */
constexpr size_t nvmeAdminRespSize = 4096U; // Size of NVMe Identify response
                                            // (bytes)

/**
 * NVMe log page size
 */
constexpr size_t nvmeLogPageSize = 512U; // Size of NVMe log pages (e.g., SMART
                                         // log)

/**
 * Type aliases for queue entries
 */
typedef struct nvme_sqe sqeType;
typedef struct nvme_cqe cqeType;

/**
 * NVMe I/O parameters for device function execution
 *
 * Contains all NVMe-specific I/O configuration parameters needed for
 * driveEndpoint execution. This POD struct can be safely passed to GPU
 * device code.
 */
struct nvmeIoParams {
  uint32_t lbaSize;       // Logical block size in bytes
  uint64_t baseLba;       // Starting LBA for I/O operations
  uint64_t lbaRangeLbas;  // LBA range limit (0 = no limit)
  bool useRandomAccess;   // true for random access, false for sequential
  int readIo;             // Number of read operations
  int writeIo;            // Number of write operations
  uint32_t lfsrSeed;      // Seed for LFSR test pattern (0 = derive from LBA)
  uint16_t queueSize;     // Queue size in entries
  uint32_t nsid;          // Namespace ID (must be > 0)
  uint32_t lbasPerIo;     // Number of LBAs per I/O operation (default: 1)
  bool infiniteMode;      // Infinite mode: run forever
  uint32_t batchSize;     // SQEs per doorbell (1=sequential, 0=all)
  uint32_t wavefrontSize; // Hardware wavefront size (threads per wave)
};

/**
 * NVMe doorbell parameters for controller notification
 *
 * Contains doorbell configuration supporting both PCI MMIO bridge mode
 * and direct BAR0 access. At least one mode must be configured.
 */
struct nvmeDoorbellParams {
  uint32_t doorbellOffset; // Offset within BAR0 for doorbell register
  uint16_t nvmeTargetBdf;  // NVMe target device BDF (0xBBDD format)
  void* shadowBufferVirt;  // Shadow buffer pointer (MMIO bridge mode)
  void* nvmeBar0Gpu;       // GPU-accessible BAR0 pointer (direct mode)
  bool usePciMmioBridge;   // Use PCI MMIO bridge mode (vs direct BAR0)
};

/**
 * NVMe data buffer parameters for read/write operations
 *
 * Contains buffer pointers and DMA addresses for data transfer operations.
 * Buffers must be GPU-accessible. DMA addresses are used for PRP entries.
 */
struct nvmeBufferParams {
  uint8_t* readBuffer;     // Read buffer pointer (can be nullptr)
  uint8_t* writeBuffer;    // Write buffer pointer (can be nullptr)
  size_t bufferSize;       // Size of buffers in bytes
  uint64_t readBufferDma;  // DMA address for read buffer
  uint64_t writeBufferDma; // DMA address for write buffer
  uint64_t* readPagePhysAddrs;
  uint64_t* writePagePhysAddrs;
  uint32_t readNumPages;
  uint32_t writeNumPages;
  uint64_t* prpListPool;
  uint64_t* prpListPageDmas;
  uint64_t prpListPoolDma;
  uint32_t prpEntriesPerCmd;
};

__host__ __device__ static inline uint32_t prpListEntriesPerPage() {
  return NVME_PAGE_SIZE / sizeof(uint64_t);
}

__host__ __device__ static inline uint64_t prpListDmaForSlot(
  const nvmeBufferParams& params, uint32_t slot) {
  if (params.prpListPageDmas)
    return params.prpListPageDmas[slot];
  if (params.prpListPoolDma && params.prpEntriesPerCmd) {
    uint32_t stride = params.prpEntriesPerCmd;
    if (stride < prpListEntriesPerPage())
      stride = prpListEntriesPerPage();
    return params.prpListPoolDma +
           (uint64_t)slot * stride * sizeof(uint64_t);
  }
  return 0;
}

/**
 * Persistent NVMe endpoint session options.
 *
 * Describes the controller, queue, GPU, memory, and verification settings used
 * to create a long-lived NVMe queue pair and GPU worker.  Unlike run(), a
 * persistent session keeps queue resources alive across submitPersistent()
 * calls.
 */
struct nvmePersistentOptions {
  const char* controller;    ///< NVMe controller path, e.g. /dev/nvme2.
  uint16_t queueId;          ///< I/O queue ID to create; 0 selects max queue.
  uint16_t queueLength;      ///< Queue length in entries; power of two.
  uint32_t nsid;             ///< Namespace identifier used for commands.
  uint32_t memoryMode;       ///< ROCm XIO memory mode bitmap.
  uint32_t lfsrSeed;         ///< LFSR seed for generated write/verify data.
  uint32_t maxTransferBytes; ///< Persistent data-buffer allocation size.
  uint32_t ringDepth;        ///< Host/GPU work ring entries.
  uint32_t batchSize;        ///< Max descriptors processed before yielding.
  uint32_t sqBatchSize;      ///< Max SQ descriptors built/submitted per pass.
  uint32_t cqBatchSize;      ///< Max CQEs polled/published per pass.
  bool precomputePrps;       ///< Use persistent-worker fast PRP setup path.
  int gpuId;                 ///< HIP GPU ID; negative leaves current device.
  bool usePciMmioBridge;     ///< Use PCI MMIO bridge instead of direct BAR0.
  bool verbose;              ///< Enable verbose ROCm XIO logging.
};

/**
 * Persistent NVMe endpoint information returned after session creation.
 */
struct nvmePersistentInfo {
  uint32_t lbaSize;       ///< Logical block size in bytes.
  uint64_t capacityLbas;  ///< Namespace capacity in LBAs.
  uint64_t capacityBytes; ///< Namespace capacity in bytes.
  uint16_t queueId;       ///< Queue ID owned by the session.
  uint16_t queueLength;   ///< Queue length in entries.
};

/**
 * Aggregate persistent-worker phase counters.
 *
 * Time fields are GPU wall-clock cycles accumulated by the persistent worker.
 * Count fields are raw event counts used to normalize per-I/O and per-batch
 * costs on the host.
 */
struct nvmePersistentPhaseStats {
  uint64_t idleWait;          ///< Cycles waiting for host work.
  uint64_t descLoad;          ///< Cycles loading/validating descriptors.
  uint64_t prpBuild;          ///< Cycles selecting buffers and building PRPs.
  uint64_t sqeBuild;          ///< Cycles filling SQE fields and LBA setup.
  uint64_t sqeWrite;          ///< Cycles writing SQEs into the submission queue.
  uint64_t sqFence;           ///< Cycles in the pre-doorbell system fence.
  uint64_t sqDoorbell;        ///< Cycles ringing the SQ tail doorbell.
  uint64_t cqPoll;            ///< Cycles polling CQEs.
  uint64_t verify;            ///< Cycles spent in optional verify work.
  uint64_t cqDoorbell;        ///< Cycles ringing the CQ head doorbell.
  uint64_t completionPublish; ///< Cycles publishing completions to host.
  uint64_t ioCount;           ///< Number of completions published.
  uint64_t batchCount;        ///< Number of non-empty batches processed.
  uint64_t submittedCount;    ///< Number of SQEs submitted.
  uint64_t completedCount;    ///< Number of successful CQEs completed.
  uint64_t pollIterations;    ///< Number of CQ poll iterations.
  uint64_t timeoutCount;      ///< Number of CQ poll timeouts.
  uint64_t errorCount;        ///< Number of completion errors.
  uint64_t maxBatch;          ///< Largest submitted batch.
  uint64_t maxPolls;          ///< Largest poll count for one CQE.
  uint32_t gpuClockKHz;       ///< GPU wall-clock frequency for conversion.
  uint32_t reserved;          ///< Reserved for ABI alignment.
};

/**
 * Host-to-GPU persistent work descriptor.
 *
 * Each descriptor represents one NVMe read or write.  The persistent worker
 * converts compact requests into SQEs, batching up to batchSize descriptors
 * per SQ/CQ doorbell pair when a persistent session enables batching.
 */
struct nvmePersistentIo {
  uint64_t userData; ///< Opaque caller tag returned in completion.
  uint64_t offset;   ///< Byte offset within the namespace.
  uint32_t len;      ///< Transfer length in bytes.
  bool isWrite;      ///< true for write, false for read.
  bool verify;       ///< Enable LFSR verification for this request.
};

/**
 * Completion returned by submitPersistent().
 */
struct nvmePersistentCompletion {
  uint64_t userData;    ///< Opaque caller tag from the descriptor.
  int error;            ///< 0 on success, errno-style value on failure.
  uint64_t bytes;       ///< Requested byte count.
  uint16_t nvmeStatus;  ///< Raw NVMe CQE status when available.
  uint64_t gpuStart;    ///< GPU wall-clock tick when SQE submission begins.
  uint64_t gpuEnd;      ///< GPU wall-clock tick when CQE processing completes.
  uint64_t gpuElapsedNs; ///< GPU-measured I/O elapsed time in nanoseconds.
  uint32_t verifyPass;  ///< Number of successful LFSR verify checks.
  uint32_t verifyFail;  ///< Number of failed LFSR verify checks.
};

/**
 * Device-visible work descriptor used by the persistent worker ring.
 *
 * This is public so integrations can reason about the ABI, but callers should
 * prefer submitPersistent() unless they intentionally own ring management.
 */
struct nvmePersistentWorkDesc {
  volatile uint64_t seq; ///< Producer sequence number.
  uint64_t userData;     ///< Opaque caller tag.
  uint64_t offset;       ///< Byte offset within the namespace.
  uint32_t len;          ///< Transfer length in bytes.
  uint8_t isWrite;       ///< Non-zero for write.
  uint8_t verify;        ///< Non-zero to request LFSR verify.
  uint8_t reserved[6];   ///< Reserved, must be zero.
};

/**
 * Device-visible completion descriptor used by the persistent worker ring.
 */
struct nvmePersistentWorkCompletion {
  volatile uint64_t seq; ///< Completion sequence number.
  uint64_t userData;     ///< Opaque caller tag.
  uint64_t bytes;        ///< Requested byte count.
  int error;             ///< 0 on success, errno-style value on failure.
  uint16_t nvmeStatus;   ///< Raw NVMe CQE status when available.
  uint8_t reserved[2];   ///< Reserved, must be zero.
  uint64_t gpuStart;     ///< GPU wall-clock tick at submit.
  uint64_t gpuEnd;       ///< GPU wall-clock tick at completion.
  uint32_t verifyPass;   ///< Number of successful LFSR verify checks.
  uint32_t verifyFail;   ///< Number of failed LFSR verify checks.
};

/**
 * Device-visible ring control block for persistent work submission.
 */
struct nvmePersistentControl {
  volatile uint64_t producer; ///< Next producer sequence.
  volatile uint64_t consumer; ///< Next consumer sequence.
  volatile uint32_t stop;     ///< Non-zero asks the worker to exit.
  uint32_t ringSize;          ///< Number of descriptors/completions.
};

/**
 * Opaque persistent NVMe endpoint session handle.
 */
struct nvmePersistentSession;

extern "C" __global__ void nvmePersistentWorkerKernel(
  xio::XioEndpointConfig config, nvmeIoParams baseParams,
  nvmeDoorbellParams doorbellParams, nvmeBufferParams bufferParams,
  volatile nvmePersistentControl* control,
  volatile nvmePersistentWorkDesc* descs,
  volatile nvmePersistentWorkCompletion* comps,
  volatile nvmePersistentPhaseStats* stats, uint32_t sqBatchSize,
  uint32_t cqBatchSize, uint32_t wavefrontSize, bool precomputePrps);

/**
 * Drive NVMe endpoint I/O operations from GPU device code
 *
 * Executes NVMe read/write operations directly from GPU kernels
 * using a unified batched loop. The batchSize parameter controls
 * how many SQEs are submitted before ringing the doorbell:
 *   batchSize 1 — one SQE per doorbell (sequential)
 *   batchSize 0 — all ops at once (fills queue)
 *   batchSize N — N SQEs per doorbell ring
 *
 * @param config Base endpoint configuration containing queue
 *               pointers, timing arrays, and common parameters.
 * @param ioParams NVMe-specific I/O parameters including LBA
 *                 configuration, operation counts, access pattern,
 *                 test pattern seed, and batchSize.
 * @param doorbellParams Doorbell configuration for notifying the
 *                       NVMe controller. Supports PCI MMIO bridge
 *                       and direct BAR0 access.
 * @param bufferParams Data buffer configuration for read/write
 *                     operations. Each SQE in a batch uses a
 *                     separate buffer region (offset b*xfer_size)
 *                     to avoid data races between in-flight I/Os.
 *
 * @note Device function — must be called from GPU kernels.
 * @note Writes execute before reads when both are requested.
 */
__device__ void driveEndpoint(const XioEndpointConfig& config,
                              const nvmeIoParams& ioParams,
                              const nvmeDoorbellParams& doorbellParams,
                              const nvmeBufferParams& bufferParams);

/**
 * Query LBA size from NVMe controller
 *
 * This function queries the NVMe controller to determine the logical block
 * size (LBA size) of namespace 1 using the Identify Namespace command.
 *
 * @param nvme_device Path to NVMe device (e.g., "/dev/nvme0")
 * @param nsid Namespace ID (default: 1)
 * @param lba_size Output parameter for LBA size in bytes
 * @return 0 on success, negative error code on failure
 */
__host__ int queryLbaSize(const char* nvme_device, uint32_t nsid,
                          unsigned* lba_size);

/**
 * Query namespace capacity in LBAs from NVMe controller
 *
 * This function queries the NVMe controller to determine the namespace
 * capacity (NSZE - Namespace Size) of namespace 1 using the Identify
 * Namespace command.
 *
 * @param nvme_device Path to NVMe device (e.g., "/dev/nvme0")
 * @param nsid Namespace ID (default: 1)
 * @param capacity_lbas Output parameter for namespace capacity in LBAs
 * @return 0 on success, negative error code on failure
 */

__host__ int queryNamespaceCapacity(const char* nvme_device, uint32_t nsid,
                                    uint64_t* capacity_lbas);

/**
 * Check if NVMe device is the root filesystem
 *
 * This function checks /proc/mounts to determine if the specified NVMe
 * device is mounted as the root filesystem. This is useful to prevent
 * accidental data corruption during testing.
 *
 * @param nvme_device Path to NVMe device (e.g., "/dev/nvme0")
 * @return 0 if device is not rootfs (safe to use), -1 if device is rootfs
 *         (unsafe), or 0 if check cannot be performed (allows to proceed)
 */
__host__ int checkRootfs(const char* nvme_device);

/**
 * Read NVMe SMART/Health Information log page
 *
 * This function reads the SMART log page (Log Identifier 0x02) from the
 * NVMe controller. The SMART log contains various device statistics including
 * data units read/written, host commands completed, power cycles, etc.
 *
 * @param nvme_device Path to NVMe device (e.g., "/dev/nvme0")
 * @param smart_log Output structure to hold SMART log data (512 bytes)
 * @return 0 on success, negative error code on failure
 */
__host__ int readSmartLog(const char* nvme_device,
                          struct nvme_smart_log* smart_log);

/**
 * Query maximum number of queues supported by NVMe controller
 *
 * This function reads the queue count from sysfs to determine the maximum
 * number of queues. The sysfs queue_count represents the total number of
 * queues (1 admin queue + N I/O queues).
 *
 * @param nvme_device Path to NVMe device (e.g., "/dev/nvme1")
 * @param max_queue_id Output parameter for maximum queue ID (last I/O queue)
 * @return 0 on success, negative error code on failure
 *
 * @note Returns the last I/O queue ID (max_queue_id). Queue 0 is admin,
 *       queues 1-N are I/O queues, so if queue_count=33, max_queue_id=32.
 */
__host__ int queryMaxQueueId(const char* nvme_device, uint16_t* max_queue_id);

/**
 * Create NVMe IO queue pair via IOCTL interface using kernel module
 *
 * This function:
 * 1. Allocates VRAM buffers for SQ and CQ using HIP
 * 2. Gets physical addresses via kernel module IOCTL
 * 3. Registers queue addresses with kernel module for kprobe injection
 * 4. Creates queues via NVMe IOCTL (CREATE_CQ and CREATE_SQ)
 * 5. The kprobe automatically injects the correct physical addresses
 *
 * @param nvme_device Path to NVMe device (e.g., "/dev/nvme0")
 * @param kernel_module_device Path to kernel module device
 *                            (e.g., "/dev/rocm-xio")
 * @param queue_id Queue ID to create (0=admin, 1+=IO queues)
 * @param queue_size Queue size in entries (must be power of 2, max 65536)
 * @param nvme_bdf NVMe device BDF in 0xBBDD format (for kernel module)
 * @param memory_mode Memory allocation mode (bits: 0=GPU write location, 1=CPU
 * write location)
 * @param info Output structure to hold queue information
 * @return 0 on success, negative error code on failure
 */
__host__ int createQueue(const char* nvme_device,
                         const char* kernel_module_device, uint16_t queue_id,
                         uint16_t queue_size, uint16_t nvme_bdf,
                         unsigned memory_mode, struct nvme_queue_info* info);

/**
 * Delete NVMe queues (SQ and CQ) for a given queue ID
 *
 * Deletes both the Submission Queue (SQ) and Completion Queue (CQ) for the
 * specified queue ID. The queues are deleted in the correct order: SQ first,
 * then CQ, as required by the NVMe specification (CQ cannot be deleted while
 * an associated SQ exists).
 *
 * Errors are logged but not fatal, as the queues may not exist. This function
 * is typically called before creating new queues to ensure a clean state.
 *
 * @param nvme_fd File descriptor for NVMe device (opened with open())
 * @param queue_id Queue ID to delete (0=admin, 1+=IO queues)
 *
 * @return 0 on success (or if queues don't exist), negative error code on
 *         fatal failure
 *
 * @note This function ignores errors from DELETE_SQ and DELETE_CQ commands,
 *       as the queues may not exist. Only fatal errors (e.g., invalid file
 *       descriptor) will cause a non-zero return value.
 */
__host__ int deleteQueue(int nvme_fd, uint16_t queue_id);

/**
 * Cleanup NVMe queues from signal handler
 *
 * This function attempts to delete NVMe queues when called from a signal
 * handler (e.g., SIGINT). It reopens the device and calls deleteQueue.
 *
 * @param endpointConfig Opaque pointer to nvmeEpConfig structure
 * @return 0 on success, negative error code on failure
 *
 * @note This function is safe to call from signal handlers (async-signal-safe)
 * @note Only attempts cleanup if queuesCreated flag is true
 */
extern "C" __host__ int nvme_ep_cleanup_queues(void* endpointConfig);

/**
 * Send NVMe CREATE_CQ and CREATE_SQ admin commands.
 *
 * Creates both CQ and SQ for the specified queue ID.
 * CQ is created first, then SQ (which references CQ).
 * Physical addresses are injected by the kernel module
 * kprobe, so virtual addresses are passed in the cmds.
 *
 * @param nvme_fd File descriptor for NVMe device.
 * @param queue_id Queue ID to create (1+ for IO).
 * @param queue_size Queue size in entries (power of 2).
 * @param sq_virt Virtual address of SQ buffer.
 * @param cq_virt Virtual address of CQ buffer.
 * @param sq_pc true for physically contiguous SQ.
 * @param cq_pc true for physically contiguous CQ.
 * @return 0 on success, negative error code on failure.
 */
__host__ int createQueueCommands(int nvme_fd, uint16_t queue_id,
                                 uint16_t queue_size, void* sq_virt,
                                 void* cq_virt, bool sq_pc, bool cq_pc);

/**
 * Create a persistent NVMe endpoint session.
 *
 * Allocates and creates one NVMe I/O queue pair, maps doorbells, allocates
 * persistent data buffers and work rings, and launches a long-lived GPU worker
 * that waits for submitPersistent() descriptors.
 *
 * @param opts Session options.
 * @param out Output session handle.
 * @return 0 on success, negative errno-style value on failure.
 */
__host__ int openPersistentSession(const nvmePersistentOptions* opts,
                                   nvmePersistentSession** out);

/**
 * Destroy a persistent NVMe endpoint session.
 *
 * Requests worker shutdown, synchronizes the HIP stream, deletes the NVMe queue
 * pair, and releases buffers and ring memory.
 *
 * @param session Session returned by openPersistentSession().
 */
__host__ void closePersistentSession(nvmePersistentSession* session);

/**
 * Query namespace and queue information for a persistent session.
 *
 * @param session Session returned by openPersistentSession().
 * @param info Output information structure.
 * @return 0 on success, negative errno-style value on failure.
 */
__host__ int getPersistentInfo(nvmePersistentSession* session,
                               nvmePersistentInfo* info);

/**
 * Query aggregate persistent-worker phase counters.
 *
 * @param session Session returned by openPersistentSession().
 * @param stats Output statistics structure.
 * @return 0 on success, negative errno-style value on failure.
 */
__host__ int getPersistentPhaseStats(nvmePersistentSession* session,
                                     nvmePersistentPhaseStats* stats);

/**
 * Reset aggregate persistent-worker phase counters.
 *
 * @param session Session returned by openPersistentSession().
 * @return 0 on success, negative errno-style value on failure.
 */
__host__ int resetPersistentPhaseStats(nvmePersistentSession* session);

/**
 * Submit one read or write to a persistent NVMe worker and wait for completion.
 *
 * The current implementation uses a single-entry ring and synchronous host
 * wait.  It avoids queue create/delete overhead but preserves a simple API for
 * fio integration.  Future versions can extend the ring size and expose
 * separate post/reap calls without changing descriptor layout.
 *
 * @param session Session returned by openPersistentSession().
 * @param io Work descriptor.
 * @param completion Completion descriptor.
 * @return 0 on success, negative errno-style value on failure.
 */
__host__ int submitPersistent(nvmePersistentSession* session,
                              const nvmePersistentIo* io,
                              nvmePersistentCompletion* completion);

/**
 * Post one descriptor to a persistent NVMe worker without waiting.
 *
 * @param session Session returned by openPersistentSession().
 * @param io Work descriptor.
 * @return 0 on success, -EAGAIN if the ring is full, or another negative
 *         errno-style value on failure.
 */
__host__ int postPersistent(nvmePersistentSession* session,
                            const nvmePersistentIo* io);

/**
 * Reap completions from a persistent NVMe worker.
 *
 * @param session Session returned by openPersistentSession().
 * @param min Minimum completions desired.
 * @param max Maximum completions to copy.
 * @param completions Completion output array.
 * @return Number of completions copied, or a negative errno-style value.
 */
__host__ int reapPersistent(nvmePersistentSession* session, uint32_t min,
                            uint32_t max,
                            nvmePersistentCompletion* completions);

/**
 * Read NVMe Submission Queue Entry (SQE) from memory
 *
 * Safely reads an entire NVMe command structure from a volatile memory
 * location. This function performs a byte-by-byte copy to ensure correct
 * behavior with volatile pointers, which is necessary when accessing
 * memory-mapped I/O regions or shared memory that may be modified by
 * hardware or other threads.
 *
 * @param sqeAddress Pointer to the volatile SQE location to read from
 * @return Copy of the SQE structure containing the NVMe command data
 *
 * @note This function is safe for use with volatile pointers and can be
 *       called from both host and device code.
 * @note The function performs a byte-by-byte copy to avoid compiler
 *       optimizations that might skip volatile reads.
 */
__host__ __device__ sqeType sqeRead(volatile sqeType* sqeAddress);

/**
 * Read NVMe Completion Queue Entry (CQE) from memory
 *
 * Safely reads an entire NVMe completion structure from a volatile memory
 * location. This function performs a byte-by-byte copy to ensure correct
 * behavior with volatile pointers, which is necessary when accessing
 * memory-mapped I/O regions or shared memory that may be modified by
 * hardware or other threads.
 *
 * @param cqeAddress Pointer to the volatile CQE location to read from
 * @return Copy of the CQE structure containing the NVMe completion data
 *
 * @note This function is safe for use with volatile pointers and can be
 *       called from both host and device code.
 * @note The function performs a byte-by-byte copy to avoid compiler
 *       optimizations that might skip volatile reads.
 */
__host__ __device__ cqeType cqeRead(volatile cqeType* cqeAddress);

/**
 * Write NVMe Submission Queue Entry (SQE) to memory
 *
 * Safely writes an entire NVMe command structure to a volatile memory
 * location. This function performs a byte-by-byte copy followed by a
 * memory fence to ensure correct ordering and visibility of the write
 * operation. This is necessary when writing to memory-mapped I/O regions
 * or shared memory that may be accessed by hardware or other threads.
 *
 * @param sqeData The SQE structure containing the NVMe command data to
 *                write
 * @param sqeAddress Pointer to the volatile SQE location to write to
 *
 * @note This function is safe for use with volatile pointers and can be
 *       called from both host and device code.
 * @note The function performs a byte-by-byte copy to avoid compiler
 *       optimizations that might skip volatile writes.
 * @note A memory fence is executed after the write to ensure the data is
 *       visible to other threads/hardware before subsequent operations.
 */
__host__ __device__ void sqeWrite(sqeType sqeData,
                                  volatile sqeType* sqeAddress);

/**
 * Write NVMe Completion Queue Entry (CQE) to memory
 *
 * Safely writes an entire NVMe completion structure to a volatile memory
 * location. This function performs a byte-by-byte copy followed by a
 * memory fence to ensure correct ordering and visibility of the write
 * operation. This is necessary when writing to memory-mapped I/O regions
 * or shared memory that may be accessed by hardware or other threads.
 *
 * @param cqeData The CQE structure containing the NVMe completion data
 *                to write
 * @param cqeAddress Pointer to the volatile CQE location to write to
 *
 * @note This function is safe for use with volatile pointers and can be
 *       called from both host and device code.
 * @note The function performs a byte-by-byte copy to avoid compiler
 *       optimizations that might skip volatile writes.
 * @note A memory fence is executed after the write to ensure the data is
 *       visible to other threads/hardware before subsequent operations.
 */
__host__ __device__ void cqeWrite(cqeType cqeData,
                                  volatile cqeType* cqeAddress);

/**
 * Poll for new SQE - waits for command ID to change
 * Simple polling function that waits until a new command appears
 *
 * @param sqeLast Last known SQE state
 * @param sqeAddress Pointer to SQE to poll
 * @return New SQE when command ID or opcode changes
 */
__host__ __device__ sqeType sqePoll(sqeType sqeLast,
                                    volatile sqeType* sqeAddress);

/**
 * Poll for new CQE - waits for command ID to change or sq_head progression
 * Enhanced polling function that checks both command ID change and sq_head
 * progression for more robust completion detection. sq_head progression is
 * the definitive indicator of completion and works regardless of which CQE
 * slot the completion appears in.
 *
 * @param cqeLast Last known CQE state
 * @param cqeAddress Pointer to CQE to poll
 * @param last_sq_head Last known submission queue head value (for sq_head
 * tracking)
 * @param sq_head_out Output parameter for new sq_head value (can be nullptr)
 * @param stopRequested Optional pointer to stop flag - if set to true, polling
 *                      will exit early (can be nullptr)
 * @return New CQE when command ID changes or sq_head increases, or last CQE
 *         if stopRequested is true
 */
__host__ __device__ cqeType
cqePoll(cqeType cqeLast, volatile cqeType* cqeAddress,
        uint16_t last_sq_head = 0, uint16_t* sq_head_out = nullptr,
        const volatile bool* stopRequested = nullptr);

/**
 * Setup NVMe Submission Queue Entry (SQE) for Read or Write command
 *
 * Initializes an NVMe command structure (SQE) with the parameters needed
 * for a read or write operation. This function configures all required
 * fields including the opcode, command ID, namespace ID, starting LBA,
 * number of logical blocks, and PRP (Physical Region Page) pointers for
 * data transfer.
 *
 * The input SQE should have the following fields pre-filled:
 * - opcode: nvme_cmd_read or nvme_cmd_write
 * - command_id: Command ID (must be unique per queue)
 * - nsid: Namespace ID (typically 1 for first namespace)
 * - dptr.prp.prp1: First PRP entry (data buffer or first page)
 * - dptr.prp.prp2: Second PRP entry (0 if single page, or PRP list)
 * - cdw10: Lower 32 bits of starting LBA (or 0, will be set from slba)
 * - cdw11: Upper 32 bits of starting LBA (or 0, will be set from slba)
 * - cdw12: Number of logical blocks minus 1 (or 0, will be set from nlb)
 *
 * This function will:
 * - Set flags, cdw2, cdw3, metadata, cdw13-15 to 0
 * - If slba is non-zero, split it across cdw10 and cdw11
 * - If nlb is non-zero, set cdw12 to nlb-1 (0-based encoding)
 *
 * @param sqe Pointer to the NVMe SQE structure to initialize (input/output)
 * @param slba Starting Logical Block Address (0 to use existing cdw10/cdw11)
 * @param nlb Number of logical blocks to read/write (0 to use existing cdw12)
 *
 * @note The function sets nlb-1 in cdw12 because NVMe uses 0-based block
 *       counts (0 means 1 block, 1 means 2 blocks, etc.).
 * @note slba is split across cdw10 (lower 32 bits) and cdw11 (upper 32 bits)
 *       to support 64-bit LBA addresses.
 * @note This function can be called from both host and device code.
 * @note If slba is 0, the function preserves existing cdw10/cdw11 values.
 * @note If nlb is 0, the function preserves existing cdw12 value.
 */
__host__ __device__ inline void sqeSetup(struct nvme_sqe* sqe, uint64_t slba,
                                         uint32_t nlb) {
  // Zero out unused/reserved fields
  sqe->flags = 0;
  sqe->cdw2 = 0;
  sqe->cdw3 = 0;
  sqe->metadata = 0;
  sqe->cdw13 = 0;
  sqe->cdw14 = 0;
  sqe->cdw15 = 0;

  // Set LBA if provided
  if (slba != 0) {
    sqe->cdw10 = (uint32_t)(slba & 0xFFFFFFFF);         // SLBA lower 32 bits
    sqe->cdw11 = (uint32_t)((slba >> 32) & 0xFFFFFFFF); // SLBA upper 32 bits
  }

  // Set number of logical blocks if provided
  if (nlb != 0) {
    sqe->cdw12 = nlb - 1;
  }
}

/**
 * Check if NVMe Completion Queue Entry (CQE) indicates successful completion
 *
 * Examines the status field of a CQE to determine if the associated command
 * completed successfully. A command is considered successful if:
 * - Status Code Type (SCT) is NVME_SCT_GENERIC (0x0)
 * - Status Code (SC) is NVME_SC_SUCCESS (0x0)
 *
 * @param cqe Pointer to the volatile CQE to check
 * @return true if the command completed successfully, false otherwise
 *
 * @note This function safely handles volatile pointers and can be called
 *       from both host and device code.
 * @note Returns false for any error condition, including command-specific
 *       errors, media errors, or vendor-specific errors.
 */
__host__ __device__ static inline bool cqeOk(const volatile cqeType* cqe) {
  uint16_t status = cqe->status;

  uint8_t sc = NVME_CQE_STATUS_SC(status);
  uint8_t sct = NVME_CQE_STATUS_SCT(status);

  return (sct == NVME_SCT_GENERIC && sc == NVME_SC_SUCCESS);
}

/**
 * Extract status code from NVMe Completion Queue Entry (CQE)
 *
 * Extracts the Status Code (SC) field from the CQE status register. The
 * status code is an 8-bit value (bits 1-8 of the 16-bit status field) that
 * indicates the specific result of the command execution. Common status codes
 * include:
 * - NVME_SC_SUCCESS (0x0): Command completed successfully
 * - NVME_SC_INVALID_OPCODE (0x1): Invalid command opcode
 * - NVME_SC_INVALID_FIELD (0x2): Invalid field in command
 * - NVME_SC_LBA_RANGE (0x80): LBA out of range
 *
 * The meaning of the status code depends on the Status Code Type (SCT), which
 * can be obtained using cqeStatusType().
 *
 * @param cqe Pointer to the volatile CQE to read from
 * @return 8-bit status code value extracted from the CQE status field
 *
 * @note This function safely handles volatile pointers and can be called
 *       from both host and device code.
 * @note The status code alone does not indicate success/failure - use
 *       cqeOk() to check for successful completion, or combine with
 *       cqeStatusType() for detailed error analysis.
 */
__host__ __device__ static inline uint8_t cqeStatusCode(
  const volatile cqeType* cqe) {
  return NVME_CQE_STATUS_SC(cqe->status);
}

/**
 * Extract status code type from NVMe Completion Queue Entry (CQE)
 *
 * Extracts the Status Code Type (SCT) field from the CQE status register.
 * The status code type is a 3-bit value (bits 9-11 of the 16-bit status
 * field) that categorizes the type of status code reported. Common status
 * code types include:
 * - NVME_SCT_GENERIC (0x0): Generic command status (most common)
 * - NVME_SCT_CMD_SPECIFIC (0x1): Command-specific status
 * - NVME_SCT_MEDIA_ERROR (0x2): Media and data integrity errors
 * - NVME_SCT_VENDOR (0x7): Vendor-specific status
 *
 * The status code type determines how to interpret the status code value
 * returned by cqeStatusCode(). For example, a status code of 0x80 means
 * different things depending on the SCT:
 * - SCT=GENERIC: NVME_SC_LBA_RANGE (LBA out of range)
 * - SCT=MEDIA_ERROR: Media error with vendor-specific details
 *
 * @param cqe Pointer to the volatile CQE to read from
 * @return 3-bit status code type value (0-7) extracted from the CQE status
 *         field
 *
 * @note This function safely handles volatile pointers and can be called
 *       from both host and device code.
 * @note Combine with cqeStatusCode() for complete error analysis, or use
 *       cqeOk() for simple success/failure checking.
 */
__host__ __device__ static inline uint8_t cqeStatusType(
  const volatile cqeType* cqe) {
  return NVME_CQE_STATUS_SCT(cqe->status);
}

/**
 * Calculate PRP entries for an NVMe data transfer with
 * full PRP list support for transfers spanning > 2 pages.
 *
 * Per NVMe spec section 4.4:
 * - <= 1 page:  PRP1 = buffer addr, PRP2 = 0
 * - <= 2 pages: PRP1 = first page, PRP2 = second page
 * - > 2 pages:  PRP1 = first page, PRP2 = physical
 *   address of a PRP list (page-aligned array of
 *   uint64_t physical page addresses for pages 2..N)
 *
 * @param bufferAddr  Physical/DMA address of the buffer
 * @param bufferSize  Transfer size in bytes
 * @param sqe         SQE to fill (prp1/prp2)
 * @param prpList     Writable PRP list memory (GPU or
 *                    host accessible). May be nullptr
 *                    when transfer fits in <= 2 pages.
 * @param prpListDma  Physical address of prpList
 * @param pagePhysAddrs  Per-page physical addresses for
 *                       non-contiguous buffers. nullptr
 *                       means contiguous from bufferAddr
 * @param bufPageOffset  Page index of the first buffer
 *                       page within pagePhysAddrs (the
 *                       buffer may start partway into
 *                       the allocation)
 */
__host__ __device__ static inline void calculatePrps(
  uint64_t bufferAddr, uint32_t bufferSize, struct nvme_sqe* sqe,
  uint64_t* prpList, uint64_t prpListDma, const uint64_t* pagePhysAddrs,
  uint32_t bufPageOffset) {
  sqe->dptr.prp.prp1 = bufferAddr;
  sqe->dptr.prp.prp2 = 0;

  uint64_t offset_in_page = bufferAddr & (NVME_PAGE_SIZE - 1);
  uint64_t first_page_size = NVME_PAGE_SIZE - offset_in_page;

  if (bufferSize <= first_page_size) {
    return;
  }

  uint32_t remaining = (uint32_t)(bufferSize - first_page_size);
  uint32_t num_remaining_pages = (remaining + NVME_PAGE_SIZE - 1) /
                                 NVME_PAGE_SIZE;

  constexpr uint32_t kMaxPrpListEntries = NVME_PAGE_SIZE / sizeof(uint64_t);
  if (num_remaining_pages > kMaxPrpListEntries) {
    num_remaining_pages = kMaxPrpListEntries;
  }

  if (num_remaining_pages == 1) {
    if (pagePhysAddrs) {
      sqe->dptr.prp.prp2 = pagePhysAddrs[bufPageOffset + 1];
    } else {
      sqe->dptr.prp.prp2 = (bufferAddr + first_page_size) &
                           ~((uint64_t)(NVME_PAGE_SIZE - 1));
    }
    return;
  }

  for (uint32_t i = 0; i < num_remaining_pages; i++) {
    if (pagePhysAddrs) {
      prpList[i] = pagePhysAddrs[bufPageOffset + 1 + i];
    } else {
      prpList[i] = ((bufferAddr + first_page_size) &
                    ~((uint64_t)(NVME_PAGE_SIZE - 1))) +
                   (uint64_t)i * NVME_PAGE_SIZE;
    }
  }

  sqe->dptr.prp.prp2 = prpListDma;
}

/**
 * Backward-compatible calculatePrps for transfers that
 * fit within at most 2 NVMe pages (up to 8KB at 4KB
 * page size). Does not support PRP lists.
 */
__host__ __device__ static inline void calculatePrps(uint64_t bufferAddr,
                                                     uint32_t bufferSize,
                                                     struct nvme_sqe* sqe) {
  sqe->dptr.prp.prp1 = bufferAddr;
  sqe->dptr.prp.prp2 = 0;

  uint64_t offset_in_page = bufferAddr & (NVME_PAGE_SIZE - 1);
  uint64_t first_page_size = NVME_PAGE_SIZE - offset_in_page;

  if (bufferSize <= first_page_size) {
    return;
  }

  sqe->dptr.prp.prp2 = (bufferAddr + first_page_size) &
                       ~((uint64_t)(NVME_PAGE_SIZE - 1));
}

/**
 * Ring NVMe doorbell with support for both PCI MMIO bridge and direct BAR0
 * modes
 *
 * @param value Doorbell value to write (sq_tail for SQ, cq_head for CQ)
 * @param doorbellParams Doorbell configuration parameters
 * @param offset_override Optional offset override (defaults to
 *                        doorbellParams.doorbellOffset). Use this for CQ
 *                        doorbell which is at SQ offset + doorBellStride.
 */
__host__ __device__ void ringDoorbell(uint16_t value,
                                      const nvmeDoorbellParams& doorbellParams,
                                      uint32_t offset_override = UINT32_MAX);

/**
 * GPU kernel entry point for NVMe endpoint operations
 *
 * This kernel function is launched from host code to execute NVMe I/O
 * operations on the GPU. It calls driveEndpoint with the provided struct
 * parameters.
 *
 * @param config Base endpoint configuration (endpointConfig field set to
 * nullptr)
 * @param ioParams NVMe I/O parameters struct
 * @param doorbellParams Doorbell configuration parameters struct
 * @param bufferParams Data buffer parameters struct
 */
__global__ void gpuKernel(XioEndpointConfig config, nvmeIoParams ioParams,
                          nvmeDoorbellParams doorbellParams,
                          nvmeBufferParams bufferParams);

/**
 * NVMe Endpoint Configuration Structure
 *
 * Contains all NVMe-specific configuration options that were previously
 * scattered in the main tester's cmdLineArgs structure. This structure
 * groups related fields using nested structs that mirror the POD structs
 * used in device code.
 */
struct nvmeEpConfig {
  // Device and queue configuration (host-side only)
  std::string controller; // NVMe controller device path
  uint16_t queueId;       // Highest queue ID (0=auto-detect, 1+=IO)
  uint16_t queueLength;   // Queue length in entries (power of 2)
  uint16_t numQueues;     // Number of queues to use (default 1)
  bool queuesCreated;     // Flag: queues have been created
  uint32_t wavefrontSize; // Hardware wavefront size

  // Per-queue state (populated by run() for multi-queue)
  struct nvme_queue_info queueInfo;
  std::vector<uint16_t> queueIds;
  std::vector<struct nvme_queue_info> queueInfos;

  // Physical addresses (host-side only)
  uint64_t doorbellAddr; // Physical address of doorbell register
  uint64_t sqBaseAddr;   // Physical base address of submission queue
  uint64_t cqBaseAddr;   // Physical base address of completion queue
  size_t sqSize;         // Size of submission queue in bytes
  size_t cqSize;         // Size of completion queue in bytes

  // I/O operation parameters (mirrors nvmeIoParams POD struct)
  struct {
    std::string accessPattern; // "sequential" or "random" (default: "random")
    unsigned lbaSize;      // LBA size in bytes (default: 512, auto-detected)
    uint64_t baseLba;      // Starting LBA for I/O operations (default: 0)
    uint64_t lbaRangeLbas; // LBA range limit (0 = no limit, default: 0)
    uint32_t lfsrSeed;     // Seed for LFSR pattern (0 = derive from LBA)
    int readIo;            // Number of read I/O operations
    int writeIo;           // Number of write I/O operations
    uint32_t nsid;         // Namespace ID (default: 1, must be > 0)
    uint32_t lbasPerIo;    // Number of LBAs per I/O (default: 1)
    bool infiniteMode;     // Infinite mode: run forever
    uint32_t batchSize;    // SQEs per doorbell (1=seq, 0=all)
  } ioParams;

  // Verification
  bool verify = false; // Verify LFSR data pattern after read-back

  // Data buffer configuration (mirrors nvmeBufferParams POD struct)
  struct {
    size_t bufferSize; // Size of data buffers in bytes
  } bufferParams;

  // Doorbell configuration (mirrors nvmeDoorbellParams POD struct)
  struct {
    bool usePciMmioBridge;  // Use PCI MMIO bridge for doorbell routing
    uint16_t mmioBridgeBdf; // PCI MMIO bridge BDF (0xBBDD format, e.g., 0x0400
                            // for 00:04.0)
    uint16_t nvmeTargetBdf; // NVMe target device BDF (0xBBDD format, e.g.,
                            // 0x0600 for 00:06.0)
    void* shadowBufferVirt; // Virtual address of PCI MMIO bridge shadow buffer
                            // (mapped from GPA)
    void* nvmeBar0Gpu;      // GPU-accessible pointer to NVMe BAR0 (for direct
                            // doorbell)
  } doorbellParams;

  // Default constructor
  // Note: queueId defaults to 0 (invalid) - will be auto-detected in
  // validateConfig
  nvmeEpConfig()
    : controller(""), queueId(0), queueLength(64), numQueues(1),
      queuesCreated(false), wavefrontSize(0), queueInfo{}, doorbellAddr(0),
      sqBaseAddr(0), cqBaseAddr(0), sqSize(0), cqSize(0),
      ioParams{"random", 512, 0, 0, 0, 0, 0, 1, 1, false, 1},
      bufferParams{1024 * 1024},
      doorbellParams{false, 0x0020, 0x0030, nullptr, nullptr} {
  }
};

/**
 * Validate NVMe endpoint configuration
 *
 * This function also auto-detects LBA size from the controller.
 * --controller is required and LBA size is always queried from it.
 *
 * @param config Pointer to nvmeEpConfig structure
 * @return Empty string if valid, error message otherwise
 */
__host__ std::string validateConfig(nvmeEpConfig* config);

/**
 * Run NVMe endpoint operations
 *
 * This function sets up queues, allocates buffers, and launches the GPU
 * kernel to execute NVMe I/O operations.
 *
 * @param config Pointer to XioEndpointConfig structure containing queue
 *               pointers and common configuration
 * @return hipSuccess on success, error code on failure
 */
__host__ hipError_t run(XioEndpointConfig* config);

} // namespace xio::nvme_ep

#endif // NVME_EP_H
